plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'org.openapi.generator' version '7.9.0'
}

group = 'org.zpi'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    mapstructVersion = "1.6.2"
    lombokVersion = "1.18.34"
    lombokMapstructBindingVersion = "0.2.0"
    set("testcontainers.version", "1.19.8")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'
    runtimeOnly 'org.postgresql:postgresql'
    implementation "org.mapstruct:mapstruct:${mapstructVersion}", "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}", "org.projectlombok:lombok:${lombokVersion}", "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign:4.1.3'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'
    implementation 'io.jsonwebtoken:jjwt:0.9.1'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'

    testImplementation("org.testcontainers:junit-jupiter")
    testImplementation("org.testcontainers:postgresql")
}

openApiGenerate {
    inputSpec.set("$rootDir/src/main/resources/openapi.yml")
    generatorName = "spring"
    apiPackage = 'org.openapitools.api'
    invokerPackage = 'org.openapitools.invoker'
    modelPackage = 'org.openapitools.model'
    configOptions = [
            useSpringBoot3: "true"
    ]
    outputDir = "$buildDir/generated"
}

sourceSets.main.java.srcDir "${openApiGenerate.outputDir.get()}/src/main/java"
sourceSets.main.resources.srcDir "${openApiGenerate.outputDir.get()}/src/main/resources"


tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('compileJava') {
    dependsOn 'openApiGenerate'
}

tasks.named('processResources') {
    dependsOn 'openApiGenerate'
}

springBoot {
    mainClass = "org.zpi.conferoapi.ConferoApiApplication"
}
