plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'org.openapi.generator' version '7.9.0'
}

group = 'org.zpi'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    springdocVersion = "2.6.0"
    mapstructVersion = "1.6.2"
    lombokVersion = "1.18.34"
    lombokMapstructBindingVersion = "0.2.0"
    feignVersion = "4.1.3"
    jjwtVersion = "0.9.1"
    jacksonDatabindNullableVersion = "0.2.6"
    testcontainersVersion = "1.19.8"
    commonsCsvVersion = "1.12.0"
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-testcontainers'

    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springdocVersion}"
    implementation "org.springframework.cloud:spring-cloud-starter-openfeign:${feignVersion}"

    implementation 'software.amazon.awssdk:s3:2.29.6'
    implementation 'org.springframework.boot:spring-boot-starter-mail'


    runtimeOnly 'org.postgresql:postgresql'

    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'
    implementation 'com.auth0:java-jwt:4.4.0'
    implementation "org.openapitools:jackson-databind-nullable:${jacksonDatabindNullableVersion}"
    implementation "org.apache.commons:commons-csv:${commonsCsvVersion}"

    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-database-postgresql'



    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    implementation "org.projectlombok:lombok:${lombokVersion}"
    implementation 'io.vavr:vavr:0.9.0'
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'io.rest-assured:rest-assured'
    testImplementation "org.testcontainers:junit-jupiter"
    testImplementation "org.testcontainers:postgresql"
}

openApiGenerate {
    inputSpec.set("$rootDir/src/main/resources/openapi.yml")
    generatorName = "spring"
    apiPackage = 'org.openapitools.api'
    invokerPackage = 'org.openapitools.invoker'
    modelPackage = 'org.openapitools.model'
    outputDir = "$buildDir/generated"

    configOptions = [
            useSpringBoot3: "true"
    ]

    typeMappings = [
            "DateTime": "Instant"
    ]

    importMappings = [
            "Instant": "java.time.Instant"
    ]
}

sourceSets {
    main {
        java.srcDir "${openApiGenerate.outputDir.get()}/src/main/java"
        resources.srcDir "${openApiGenerate.outputDir.get()}/src/main/resources"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('compileJava') {
    dependsOn 'openApiGenerate'
}

tasks.named('processResources') {
    dependsOn 'openApiGenerate'
}

springBoot {
    mainClass = "org.zpi.conferoapi.ConferoApiApplication"
}
